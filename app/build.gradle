apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'
apply plugin: 'jacoco'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.github.ben-manes.versions'
//apply plugin: 'io.fabric'

def appVersionCode = getVersionCode()
def appVersionName = new Date().format("yyMMdd") + "-" + appVersionCode

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    defaultConfig {
        defaultPublishConfig 'debug'

        applicationId "com.alexmihai.mvp"
        minSdkVersion 21
        targetSdkVersion 27
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
        versionCode appVersionCode
        versionName appVersionName

        buildConfigField("String", "POKEAPI_API_URL", "\"${PokeapiApiUrl}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.squareup.okio:okio:1.11.0'
        force "com.squareup.okhttp3:okhttp:3.5.0"
    }
}


apply from: 'dependencies.gradle'

dependencies {

    implementation supportLibs
    implementation networkLibs
    implementation rxJavaLibs
    implementation googlePlayLibs

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    //RxPermission for easy runtime permission management
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'

    implementation "com.jakewharton:butterknife:$versions.butterKnife"
    implementation "com.jakewharton.timber:timber:4.5.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.0"

    debugImplementation 'com.readystatesoftware.chuck:library:1.0.4'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.0.4'

    debugImplementation('com.github.ajitsing:sherlock:1.0.2@aar') {
        transitive = true
    }
    releaseImplementation ('com.github.ajitsing:sherlock-no-op:1.0.2@aar')

    implementation 'com.tspoon.traceur:traceur:1.0.1'

    implementation "com.google.dagger:dagger:$versions.dagger"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    debugImplementation leakCannary
    releaseImplementation leakCannary
    testImplementation leakCannary

    // APT dependencies
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterKnife"
    annotationProcessor daggerCompiler
    testAnnotationProcessor daggerCompiler
    androidTestAnnotationProcessor daggerCompiler

    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

static def getVersionCode() {
    def buildNumber = System.getenv('BUILD_NUMBER')

    if (buildNumber != null && buildNumber.isNumber()) {
        return buildNumber.toInteger()
    }
    return 4
}

// Google Play publisher configuration
play {
    track = getPlayTrack() // 'production', 'rollout', 'beta' or 'alpha'
    // only necessary for 'rollout', in this case default is 0.2 (20% of the target)
    userFraction = 0.2
    untrackOld = true // will untrack 'alpha' while upload to 'beta'
    uploadImages = false
}

static def getPlayTrack() {
    def track = System.getenv('RELEASE_TRACK')

    if (track != null) {
        return track
    }
    return 'alpha'
}
